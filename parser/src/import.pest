main = {
  SOI ~ ( ignore_string | import_block | inline_import | match_any )+ ~ EOI
}

WS = _{ ( " " | "\n" )* }
STR = _{ ( "\"" | "'" ) }

file_name = {
    ( !( "\"" | "'" ) ~ ANY )+
}

varlist = _{
    ( "\n" | "\r" | " " | "," )
}

variable_def = _{
   ( !( "\n" | "\r" | " " | "," | "}" ) ~ ANY )+
}

name = {
   variable_def
}

output = {
    variable_def
}

variable = {
    name+ ~ ( " "+ ~ "as" ~ " "+ ~ output )? ~ varlist*
}

from = {
  "from"
}

start_variable = {
    "{"
}

end_variable = {
    "}"
}

import_block = {
      "await"? ~ WS ~ "import" ~ WS ~
      ( start_variable ~ varlist* ~ variable* ~ end_variable ~ WS ~ from )?
      ~ WS
      ~ ( "(" ~ WS )?
      ~ STR ~ file_name ~ STR
      ~ ( WS ~ ")")?
      ~ WS ~ ";"
}

inline_import = {
    "import" ~ WS
        ~ "(" ~ WS
        ~ STR ~ file_name ~ STR
        ~ WS
        ~ ")" ~ WS
}

ignore_string_1 = _{
	"`" ~ ( !"`" ~ ANY )* ~ "`"
}

ignore_string_2 = _{
	"\"" ~ ( !"\"" ~ ANY )* ~ "\""
}

ignore_string_3 = _{
	"'" ~ ( !"'" ~ ANY )* ~ "'"
}

ignore_string = {
	( ignore_string_1 | ignore_string_2 | ignore_string_3 )
}

match_any = {
    ANY
}
